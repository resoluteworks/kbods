plugins {
    id "maven-publish"
    id "signing"
    id "com.glovoapp.semantic-versioning" version "1.1.10"
    id "org.jetbrains.kotlin.jvm" version "${kotlinVersion}" apply false
    id "org.jetbrains.dokka" version "1.7.20" apply false
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: 'org.jetbrains.kotlin.jvm'

    group = "io.resoluteworks"

    java {
        withJavadocJar()
        withSourcesJar()
    }

    javadocJar {
        dependsOn(dokkaHtml)
        archiveClassifier.set("javadoc")
        from(dokkaHtml.outputDirectory)
    }

    repositories {
        mavenLocal()
        mavenCentral()
    }

    configurations.all {
        resolutionStrategy {
            force 'it.unibz.inf.ontop:ontop-rdf4j-config-sql:4.2.2'
            force 'it.unibz.inf.ontop:ontop-ontology-owlapi:4.2.2'
            force 'it.unibz.inf.ontop:ontop-system-sql-owlapi:4.2.2'
        }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
        implementation "org.slf4j:slf4j-api:1.7.36"
        implementation "com.beust:klaxon:5.6"

        testImplementation "org.testng:testng:7.7.0"
        testImplementation "org.jetbrains.kotlin:kotlin-test:${kotlinVersion}"
        testImplementation "org.awaitility:awaitility:4.2.0"
        testImplementation "ch.qos.logback:logback-classic:1.4.5"
        testImplementation "io.github.serpro69:kotlin-faker:1.10.0"
        testImplementation 'org.testcontainers:testcontainers:1.17.6'
    }

    kotlin {
        jvmToolchain(11)
    }

    test {
        useTestNG()
        testLogging {
            showStandardStreams = true
        }
    }

    publishing {
        repositories {
            mavenLocal()
            maven {
                url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                credentials {
                    username = System.getenv('NEXUS_USERNAME')
                    password = System.getenv('NEXUS_PASSWORD')
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name = "${project.name}"
                    description = "${projectDescription}"

                    url = "https://github.com/${publishGit}"
                    licenses {
                        license {
                            name = 'Apache 2.0'
                            url = "https://github.com/${publishGit}/blob/main/LICENSE"
                            distribution = 'repo'
                        }
                    }
                    scm {
                        url = "https://github.com/${publishGit}"
                        connection = "scm:git:git://github.com/${publishGit}.git"
                        developerConnection = "scm:git:ssh://git@github.com:${publishGit}.git"
                    }
                    developers {
                        developer {
                            name = 'Cosmin Marginean'
                        }
                    }
                }
            }
        }
    }
}
